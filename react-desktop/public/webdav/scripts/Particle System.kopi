Point = struct (Number, Number)

extend Point (
  +: (that) => Point (this.0 + that.0, this.1 + that.1)
)

gravity = Point (0, 0.01)

Particle = struct (
  position: Point
  velocity: Point
  ttl: Number
)

generateParticle () = {
  Particle (
    position: Point (180, 150)
    velocity: Point (random -0.25..0.25, random -1.5..-1)
    ttl: random 100..200
  )
}

initialState = 1..20 | map generateParticle | toArray

ParticleSystem = component (setState) => {
  (particles = initialState) => {
    requestAnimationFrame () => {
      particles = particles | map (position, velocity, ttl) =>
        ttl < 0 ? generateParticle () : Particle (
          position: position + velocity
          velocity: velocity + gravity
          ttl: ttl - 1
        ) | toArray
      
      setState particles
    }

    Svg () {
      particles | map (position, velocity) => {
        Circle (cx: position.0, cy: position.1, r: 2)
      } | toArray
    }
  }
}

ParticleSystem () []
