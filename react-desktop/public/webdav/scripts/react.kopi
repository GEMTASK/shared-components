Vector = struct (Number, Number)

extend Vector (
  +: (that) => Vector (this.0 + that.0, this.1 + that.1)
)

Particle = struct (
  position: Vector
  velocity: Vector
)

generateParticle () = {
  Particle (
    position: Vector (180, 150)
    velocity: Vector (random -0.25..0.25, random -1.5..-1)   
  )
}

initialState = 1..10 | map generateParticle | toArray

gravity = Vector (0, 0.01)

ParticleSystem = component (setState) => {
  (particles = initialState) => {
    requestAnimationFrame () => {
      setState (
        particles | map (position, velocity) => Particle (
          position: position + velocity
          velocity: velocity + gravity
        ) | toArray
      )
    }

    Svg () {
      particles | map (position, velocity) => {
        Circle (cx: position.0, cy: position.1, r: 2)
      } | toArray
    }
  }
}

ParticleSystem () []
